import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "org.openapi.generator" version "7.0.1"
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "$buildDir/generated/openapi/main/java"]
        }
    }
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.projectlombok:lombok:1.18.30'
    implementation 'org.jooq:jooq:3.18.7'
    implementation 'org.springdoc:springdoc-openapi-common:1.6.13'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.servlet:servlet-api:2.5'
    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'mysql:mysql-connector-java:8.0.28'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:postgresql:1.17.6"
    testImplementation 'org.testcontainers:mysql:1.17.6'
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
}

tasks.named('test') {
    useJUnitPlatform()
}


task generateOperatorOpenApi(type: GenerateTask) {
    generatorName = "spring"
    inputSpec = "$projectDir/src/main/resources/openapi.yaml".toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "com.example.demo.openapi.api"
    invokerPackage = "com.example.demo.openapi.invoker"
    modelPackage = "com.example.demo.openapi.model"
    modelNameSuffix = "Dto"
    configOptions = [
            dateLibrary            : "java8",
            interfaceOnly          : "true",
            useTags                : "true",
            skipDefaultInterface   : "true",
            hideGenerationTimestamp: "true",
            implicitHeaders        : "true",
            sourceFolder           : "main/java"
    ]
    groupId = "com.example.demo.openapi"
}

openApiValidate {
    inputSpec = "$projectDir/src/main/resources/openapi.yaml".toString()
}

task buildOpenapi() {
    dependsOn tasks.generateOperatorOpenApi
    dependsOn tasks.openApiValidate
}

compileJava.dependsOn buildOpenapi